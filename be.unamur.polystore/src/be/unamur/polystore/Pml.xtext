grammar be.unamur.polystore.Pml with org.eclipse.xtext.common.Terminals

generate pml "http://www.unamur.be/polystore/Pml"

Domainmodel:
    conceptualSchema=ConceptualSchema & physicalSchema=AbstractPhysicalSchema & mappingRules+= MappingRules
    
;

ConceptualSchema :
	'conceptual' 'schema' name=ID '{'
		(entities+=EntityType)+ & (relationships+=RelationshipType)*
	'}'
;

EntityType returns EntityType:
	'entity' 'type' name=ID '{'
		(attributes+=Attribute (','attributes+=Attribute)*)?
		'}'
;
 
RelationshipType:
	'relationship' 'type' name=ID '{'
	(roles+=Role (','roles+=Role)*)+
	(','attributes+=Attribute)*
'}'
;

Role:
	name = ID ('['cardinality=Cardinality']') ':' entity=[EntityType]
;

Attribute:
	name=ID ':' (type=DataType)
;

enum Cardinality returns Cardinality:
				ZERO_ONE = '0-1' | ONE = '1' | ZERO_MANY = '0-N' | ONE_MANY = '1-N';

QualifiedName:
    ID ('.' ID)*
;

AbstractPhysicalSchema:
	{AbstractPhysicalSchema} 'physical' 'schemas' '{' 
		(kvschemas+=KeyValueSchema* & documentschemas+= DocumentSchema* & relationalschemas+=RelationalSchema* & graphschemas+= GraphSchema* & columnschemas += ColumnSchema*)
	'}'
;

DocumentSchema:
	'document' 'schema' SchemaName '{'
	(collections+=Collection)*
'}'
;

Collection:
	'collection' name=ID '{'
		 'fields' '{'
		(fields+=PhysicalField (',' fields+=PhysicalField)*)?
	'}'
		('references' '{'references+=Reference*'}' )?
	'}'
;

Reference:
	name=ID ':' sourceField=[PhysicalField|QualifiedName] '->' targetField=[PhysicalField|QualifiedName]
;


PhysicalField returns PhysicalField:
	ShortField | EmbeddedObject
;

EmbeddedObject:
	name=ID '['cardinality=Cardinality']'('{'
		(fields+=PhysicalField (',' fields+=PhysicalField)*)?
	'}')?
;

ShortField :
	name=ID
;


ColumnSchema:
	'column' 'schema' SchemaName '{}'
;


GraphSchema:
	'graph' 'schema' SchemaName '{}'
;

RelationalSchema:
	'relational' 'schema' SchemaName '{'
		tables+=Table*
	'}'
;

Table:
	'table' name=ID '{'
		'columns' '{'
			(columns+=ShortField (',' columns+=ShortField)*)?
			'}'
		('references' '{'references+=Reference*'}')? 
		//Identifier and index section
	'}'
;

AbstractPhysicalStructure:
	Table | Collection 
;

KeyValueSchema:
	'key' 'value' 'schema' SchemaName '{}'
;

MappingRules:
	{MappingRules} 'mapping' 'rules' '{'
		(mappingRules+=MappingRule (','mappingRules+=MappingRule)*)?
	'}'
;


MappingRule:
	(entityConceptual=[EntityType|QualifiedName]'('(attributesConceptual+=[Attribute|QualifiedName] (','attributesConceptual+=[Attribute|QualifiedName])*)?')''->' physicalStructure=[AbstractPhysicalStructure|QualifiedName]'('(physicalFields+=[PhysicalField|QualifiedName] (','physicalFields+=[PhysicalField|QualifiedName])*)?')')
;

SchemaName:
	name=ID
;

DataType returns DataType:
	IntType | 
	BigintType | 
	StringType | 
	TextType | 
	PointType | 
	PolygonType | 
	BoolType | 
	FloatType | 
	BlobType | 
	DateType | 
	DatetimeType
	;

IntType returns IntType:
	{IntType}
	'int'
	;

BigintType returns BigintType:
	{BigintType}
	'bigint'
	;

StringType returns StringType:
	{StringType}
	'string' ('[' maxSize=INT ']')?
	;

TextType returns TextType:
	{TextType}
	'text'
	;

PointType returns PointType:
	{PointType}
	'point'
	;

PolygonType returns PolygonType:
	{PolygonType}
	'polygon'
	;

BoolType returns BoolType:
	{BoolType}
	'bool'
	;

FloatType returns FloatType:
	{FloatType}
	'float'
	;

BlobType returns BlobType:
	{BlobType}
	'blob'
	;


DateType returns DateType:
	{DateType}
	'date'
	;

DatetimeType returns DatetimeType:
	{DatetimeType}
	'datetime'
	;